%include "./sdl.hasm"

%const SCREEN_WIDTH  = 800
%const SCREEN_HEIGHT = 600

%const EVENT = byte_array(SDL_Event_size, 0)

%const LOGO_WIDTH  = 69
%const LOGO_HEIGHT = 69
%const LOGO_RECT   = byte_array(SDL_Rect_size, 0)
%const LOGO_SPEED  = 1
%const LOGO_DX     = int32(LOGO_SPEED)
%const LOGO_DY     = int32(LOGO_SPEED)
%const LOGO_COLOR  = 0xFF0000FF

%const RGBA_COMP = 4

split_colors:
%scope
    swap 1
    %for i from 1 to RGBA_COMP
        dup 0
        push (i - 1) * 8
        shr
        push 0xFF
        andb
        swap 1
    %end
    drop
    %for i from 1 to RGBA_COMP
        swap i
    %end
    ret
%end


init_logo:
%scope
    push LOGO_RECT + SDL_Rect_x
    push 100
    write32

    push LOGO_RECT + SDL_Rect_y
    push 100
    write32

    push LOGO_RECT + SDL_Rect_w
    push LOGO_WIDTH
    write32

    push LOGO_RECT + SDL_Rect_h
    push LOGO_HEIGHT
    write32

    ret
%end

render_logo:
%scope
    swap 1

    dup 0
    push LOGO_COLOR
    call split_colors
    native SDL_SetRenderDrawColor
    drop

    dup 0
    push LOGO_RECT
    native SDL_RenderFillRect
    drop

    drop

    ret
%end

;; TODO(#279): logo does not bounce off of the edge of the screen
update_logo:
%scope
    %scope
        push LOGO_RECT + SDL_Rect_y
        read32
        push SCREEN_HEIGHT
        lti
        jmp_if skip
            push LOGO_DY
            read32
            push -1
            multi
            push LOGO_DY
            swap 1
            write32
        skip:
    %end

    %scope
        push LOGO_RECT + SDL_Rect_x
        read32
        push SCREEN_WIDTH
        lti
        jmp_if skip
            push LOGO_DX
            read32
            push -1
            multi
            push LOGO_DX
            swap 1
            write32
        skip:
    %end

    push LOGO_RECT + SDL_Rect_x
    ;; TODO(#288): read32 (and others) don't read the signed numbers properly
    ;; Because of that the example accedentally works without checking all of the "wall" cases. 
    read32
    push LOGO_DX
    read32
    plusi
    push LOGO_RECT + SDL_Rect_x
    swap 1
    write32

    push LOGO_RECT + SDL_Rect_y
    read32
    push LOGO_DY
    read32
    plusi
    push LOGO_RECT + SDL_Rect_y
    swap 1
    write32

    ret
%end

%entry main:
    push SDL_INIT_VIDEO
    native SDL_Init
    drop

    push 0 ;; x
    push 0 ;; y
    push SCREEN_WIDTH
    push SCREEN_HEIGHT
    push SDL_WINDOW_RESIZABLE
    native SDL_CreateWindow

    native SDL_CreateRenderer

    call init_logo

    main_loop:
        loop:
            push EVENT
            native SDL_PollEvent

            push EVENT
            read32

            push SDL_QUIT
            equ
            jmp_if end

        jmp_if loop

        call update_logo

        ;; SDL_SetRenderDrawColor(renderer, 255, 0, 0, 255);
        dup 0
        push 0xFF181818
        call split_colors
        native SDL_SetRenderDrawColor
        drop

        ;; SDL_RenderClear(renderer);
        dup 0
        native SDL_RenderClear
        drop

        dup 0
        call render_logo

        ;; SDL_RenderPresent(renderer);
        dup 0
        native SDL_RenderPresent
    jmp main_loop
    end:
    native SDL_Quit
    halt
